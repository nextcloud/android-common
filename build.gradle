/*
 * Nextcloud Android Common Library
 *
 * SPDX-FileCopyrightText: 2022-2023 Nextcloud GmbH and Nextcloud contributors
 * SPDX-License-Identifier: MIT
 */

buildscript {
    ext {
        daggerVersion = "2.59"
        kotlinVersion = "2.3.0"
        jUnitVersion = "4.13.2"
        androidXTestVersion = "1.3.0"
        androidXEspressoTestVersion = "3.7.0"
        mockkVersion = "1.14.9"
        mockitoVersion = "5.21.0"
        mockitoKotlinVersion = "6.2.3"
    }
}

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'org.jetbrains.kotlin.plugin.compose' version '2.3.0' apply false
    id 'com.android.library' version '9.0.0' apply false
    id 'org.jetbrains.kotlin.android' version "$kotlinVersion" apply false
    id 'com.android.legacy-kapt' version '9.0.0' apply false
    id 'com.diffplug.spotless' version '8.1.0' apply false
    id 'io.gitlab.arturbosch.detekt' version '1.23.8' apply false
    id 'com.android.application' version '9.0.0' apply false
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

tasks.register('installGitHooks', Copy) {
    def sourceFolder = "${rootProject.projectDir}/scripts/hooks"
    def destFolder = "${rootProject.projectDir}/.git/hooks"

    description = "Install git hooks"
    group = "development"

    from(sourceFolder) {
        include '*'
    }
    into destFolder

    eachFile { file ->
        println "${sourceFolder}/${file.relativeSourcePath} -> ${destFolder}/${file.path}"
    }
}

allprojects {
    group = 'com.nextcloud.android-common'
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'io.gitlab.arturbosch.detekt'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint()
        }
    }
    detekt {
        buildUponDefaultConfig = true
    }
}
