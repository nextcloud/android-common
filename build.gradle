buildscript {
    ext {
        daggerVersion = "2.47"
        kotlinVersion = "1.9.0"
    }
}

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.library' version '7.4.0' apply false
    id 'org.jetbrains.kotlin.android' version "$kotlinVersion" apply false
    id "org.jetbrains.kotlin.kapt" version "$kotlinVersion" apply false
    id "com.diffplug.spotless" version "6.20.0" apply false
    id 'io.gitlab.arturbosch.detekt' version '1.23.0' apply false
    id 'com.android.application' version '7.4.0' apply false
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task installGitHooks(type: Copy, group: "development") {
    def sourceFolder = "${rootProject.projectDir}/scripts/hooks"
    def destFolder = "${rootProject.projectDir}/.git/hooks"

    description = "Install git hooks"

    from(sourceFolder) {
        include '*'
    }
    into destFolder

    eachFile { file ->
        println "${sourceFolder}/${file.relativeSourcePath} -> ${destFolder}/${file.path}"
    }
}


allprojects {
    group = 'com.nextcloud.android-common'
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'io.gitlab.arturbosch.detekt'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint()
        }
    }
    detekt {
        buildUponDefaultConfig = true
    }
}
