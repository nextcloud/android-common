/*
 * Nextcloud Android Common Library
 *
 * Copyright (C) 2022-2023 Nextcloud GmbH
 *
 * SPDX-FileCopyrightText: 2022-2023 Nextcloud GmbH and Nextcloud contributors
 * SPDX-License-Identifier: GPL-3.0-or-later
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program. If not, see <https://www.gnu.org/licenses/>.
 */

buildscript {
    ext {
        daggerVersion = "2.51"
        kotlinVersion = "1.9.22"
    }
}

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.library' version '8.2.2' apply false
    id 'org.jetbrains.kotlin.android' version "$kotlinVersion" apply false
    id "org.jetbrains.kotlin.kapt" version "$kotlinVersion" apply false
    id "com.diffplug.spotless" version "6.25.0" apply false
    id 'io.gitlab.arturbosch.detekt' version '1.23.5' apply false
    id 'com.android.application' version '8.2.2' apply false
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

tasks.register('installGitHooks', Copy) {
    def sourceFolder = "${rootProject.projectDir}/scripts/hooks"
    def destFolder = "${rootProject.projectDir}/.git/hooks"

    description = "Install git hooks"
    group = "development"

    from(sourceFolder) {
        include '*'
    }
    into destFolder

    eachFile { file ->
        println "${sourceFolder}/${file.relativeSourcePath} -> ${destFolder}/${file.path}"
    }
}


allprojects {
    group = 'com.nextcloud.android-common'
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'io.gitlab.arturbosch.detekt'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint()
        }
    }
    detekt {
        buildUponDefaultConfig = true
    }
}
